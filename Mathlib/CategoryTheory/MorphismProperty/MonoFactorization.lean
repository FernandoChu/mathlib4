/-
Copyright (c) 2019 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Markus Himmel
-/
import Mathlib.CategoryTheory.Category.Basic
import Mathlib.CategoryTheory.Comma.Arrow
import Mathlib.CategoryTheory.EqToHom

/-!
# Mono Factorizations

A `MonoFactorization` is a factorization `f = e ‚â´ m`, where `m` is a monomorphism.

## Future work
* TODO: Connect definitions with `CategoryTheory/MorphismProperty/Factorization.lean`

-/

noncomputable section

universe v u

open CategoryTheory

namespace CategoryTheory.Limits

variable {C : Type u} [Category.{v} C]
variable {X Y : C} (f : X ‚ü∂ Y)

/-- A factorization of a morphism `f = e ‚â´ m`, with `m` monic. -/
structure MonoFactorization where
  I : C
  m : I ‚ü∂ Y
  [m_mono : Mono m]
  e : X ‚ü∂ I
  fac : e ‚â´ m = f := by aesop_cat

attribute [inherit_doc MonoFactorization] MonoFactorization.I MonoFactorization.m
  MonoFactorization.m_mono MonoFactorization.e MonoFactorization.fac

attribute [reassoc (attr := simp)] MonoFactorization.fac

attribute [instance] MonoFactorization.m_mono

@[deprecated (since := "2025-08-01")] alias MonoFactorisation := MonoFactorization

namespace MonoFactorization

/-- The obvious factorization of a monomorphism through itself. -/
def self [Mono f] : MonoFactorization f where
  I := X
  m := f
  e := ùüô X

instance [Mono f] : Inhabited (MonoFactorization f) := ‚ü®self f‚ü©

variable {f}

/-- The morphism `m` in a factorization `f = e ‚â´ m` through a monomorphism is uniquely
determined. -/
@[ext (iff := false)]
theorem ext {F F' : MonoFactorization f} (hI : F.I = F'.I)
    (hm : F.m = eqToHom hI ‚â´ F'.m) : F = F' := by
  obtain ‚ü®_, Fm, _, Ffac‚ü© := F; obtain ‚ü®_, Fm', _, Ffac'‚ü© := F'
  cases hI
  simp? at hm says simp only [eqToHom_refl, Category.id_comp] at hm
  congr
  apply (cancel_mono Fm).1
  rw [Ffac, hm, Ffac']

/-- Any mono factorization of `f` gives a mono factorization of `f ‚â´ g` when `g` is a mono. -/
@[simps]
def compMono (F : MonoFactorization f) {Y' : C} (g : Y ‚ü∂ Y') [Mono g] :
    MonoFactorization (f ‚â´ g) where
  I := F.I
  m := F.m ‚â´ g
  m_mono := mono_comp _ _
  e := F.e

/-- A mono factorization of `f ‚â´ g`, where `g` is an isomorphism,
gives a mono factorization of `f`. -/
@[simps]
def ofCompIso {Y' : C} {g : Y ‚ü∂ Y'} [IsIso g] (F : MonoFactorization (f ‚â´ g)) :
    MonoFactorization f where
  I := F.I
  m := F.m ‚â´ inv g
  m_mono := mono_comp _ _
  e := F.e

/-- Any mono factorization of `f` gives a mono factorization of `g ‚â´ f`. -/
@[simps]
def isoComp (F : MonoFactorization f) {X' : C} (g : X' ‚ü∂ X) : MonoFactorization (g ‚â´ f) where
  I := F.I
  m := F.m
  e := g ‚â´ F.e

/-- A mono factorization of `g ‚â´ f`, where `g` is an isomorphism,
gives a mono factorization of `f`. -/
@[simps]
def ofIsoComp {X' : C} (g : X' ‚ü∂ X) [IsIso g] (F : MonoFactorization (g ‚â´ f)) :
    MonoFactorization f where
  I := F.I
  m := F.m
  e := inv g ‚â´ F.e

/-- If `f` and `g` are isomorphic arrows, then a mono factorization of `f`
gives a mono factorization of `g` -/
@[simps]
def ofArrowIso {f g : Arrow C} (F : MonoFactorization f.hom) (sq : f ‚ü∂ g) [IsIso sq] :
    MonoFactorization g.hom where
  I := F.I
  m := F.m ‚â´ sq.right
  e := inv sq.left ‚â´ F.e
  m_mono := mono_comp _ _
  fac := by simp only [fac_assoc, Arrow.w, IsIso.inv_comp_eq, Category.assoc]

end CategoryTheory.Limits.MonoFactorization
